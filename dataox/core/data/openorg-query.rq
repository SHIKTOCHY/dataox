CONSTRUCT {
  ?uni a ?type ;
    rdfs:label ?label ;
    foaf:logo ?logo ;
    skos:prefLabel ?label ;
    skos:altLabel ?altLabel ;
    skos:hiddenLabel ?hiddenLabel ;
    owl:sameAs ?sameAs ;
    foaf:homepage ?homepage ;
    foaf:based_near ?point ,
      <http://dbpedia.org/resource/Oxford> ;
    foaf:account ?account ;
    ?lyouProperty ?lyouValue .
  ?account a foaf:OnlineAccount ;
    foaf:accountName ?accountName ;
    foaf:accountServiceHomepage ?accountServiceHomepage .
  ?point a geo:Point ;
    geo:lat ?lat ;
    geo:long ?long .
  ?dataset a dcat:Dataset ;
    oo:organization ?uni ;
    oo:contact ?datasetContact ;
    oo:corrections ?datasetCorrections ;
    dcterms:subject ?datasetSubject ;
    dcterms:conformsTo ?datasetConformsTo ;
    dcterms:license ?datasetLicense .
  ?datasetCorrections a foaf:Agent ;
    foaf:mbox ?datasetCorrectionsEmail .
  ?datasetContact a foaf:Agent ;
    foaf:mbox ?datasetContactEmail .
} WHERE {
  BIND (%(uni)s AS ?uni) .
  ?uni a ?type ;
  OPTIONAL { ?uni foaf:logo ?logo }
  OPTIONAL {
    ?uni skos:prefLabel ?_label
    BIND(STR(?_label) AS ?label)
  }
  OPTIONAL { ?uni skos:altLabel ?altLabel }
  OPTIONAL { ?uni skos:hiddenLabel ?hiddenLabel }
  OPTIONAL { ?uni owl:sameAs|skos:exactMatch ?sameAs }
  OPTIONAL { ?uni foaf:homepage ?homepage }
  OPTIONAL { ?uni org:hasPrimarySite [ geo:lat ?lat ; geo:long ?long ] }
  OPTIONAL {
    ?uni foaf:account ?account .
    ?account a foaf:OnlineAccount ;
      foaf:accountName ?accountName ;
      foaf:accountServiceHomepage ?accountServiceHomepage .
  }
  OPTIONAL {
    ?lyouProperty rdfs:isDefinedBy lyou: .
    ?uni ?lyouProperty ?lyouValue
  }
  OPTIONAL {
    ?dataset a void:Dataset ;
      oo:organization ?uni ;
      dcterms:subject ?datasetSubject ;
      dcterms:conformsTo ?datasetConformsTo ;
      dcterms:license ?datasetLicense .
    OPTIONAL {
      ?dataset oo:contact ?datasetContact
      OPTIONAL { ?datasetContact v:email|foaf:mbox ?datasetContactEmail }
    }
    OPTIONAL {
      ?dataset oo:corrections ?datasetCorrections
      OPTIONAL { ?datasetCorrections v:email|foaf:mbox ?datasetCorrectionsEmail }
    }
  }
}
